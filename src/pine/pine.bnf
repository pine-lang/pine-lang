OPERATIONS := OPERATION <whitespace*> (<"|"> <whitespace*> OPERATION <whitespace*>)*
OPERATION  := SELECT | RESOURCE | LIMIT | GROUP | ORDER | META | DELETE | SET | FUNCTION

SELECT          := specific | invert-specific
specific        := <#"s(el(ect)?)?:"> <whitespace+> columns
invert-specific := <#"u(ns(elect)?)?:"> <whitespace+> columns
columns         := column (<","> <whitespace*> column)*
column          := string | string <whitespace+> <"as"> <whitespace+> alias
alias           := string

RESOURCE        := entity | entity <whitespace+> id | entity <whitespace+> ids | entity <whitespace+> ands | entity <whitespace+> ors

ORDER           := <#"o(rder)?:"> <whitespace*> "+"? column

GROUP           := <#"g(roup)?:"> <whitespace*> column (<whitespace+> FUNCTION)?
FUNCTION        := #"(count|min|max|join)" <":"> <whitespace+> column

LIMIT           := <#"l(imit)?:"> <whitespace*> number

META            := ref
ref             := <#"ref(erences)?"> <"?">

DELETE          := <#"delete!">

SET             := <#"set!"> <whitespace*> assignments

id           := number
assignments  := assignment (<whitespace*> assignment)*
assignment   := string <whitespace*> <"="> <whitespace*> string

ids         := number (<whitespace*> <","> <whitespace*> number)*
ors         := comparison (<whitespace*> <","> <whitespace*> comparison)*
ands        := comparison (<whitespace+> comparison)*
comparison  := string <whitespace*> <"="> <whitespace*> ids | string <whitespace*> operator <whitespace*> (quoted-string|number) | string "?"  | "!" string "?"


number        := #"[0-9]+"
whitespace    := #"\s+"
entity        := string
operator      := #"<|>|="

quoted-string := <"'"> space-string? <"'"> | <'"'> space-string? <'"'>
space-string  := #"[A-Za-z0-9-_*.@ ]+"
string        := #"[A-Za-z0-9-_*.@]+"